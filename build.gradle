plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'maven-publish'
}

ext.url = 'https://qsml.dualspiral.co.uk'

group 'uk.co.drnaylor'
version '0.12.1-SNAPSHOT'

defaultTasks 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api "org.spongepowered:configurate-core:3.7.1"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testImplementation "org.spongepowered:configurate-gson:3.7.1"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:1.10.19"
}

license {
    ext.name = 'QuickStart Module Loader'

    exclude "**/*.info"
    exclude "**/*.json"
    exclude "assets/**"
    exclude "*.properties"

    header file('HEADER.txt')
    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

blossom {
    def location = 'src/main/java/uk/co/drnaylor/quickstart/Metadata.java'

    // replaceToken '@id@', project.name.toLowerCase(), location
    replaceToken '@name@', 'QuickStart Module Loader', location
    replaceToken '@version@', project.version, location
    replaceToken '@informativeVersion@', project.version + "+" + getGitHash(), location
}

jar {
    manifest {
        attributes  'Implementation-Title': project.name,
                'Implementation-Version': archiveVersion,
                'Git-Hash': getGitHash()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

private String getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute();
    process.waitFor();
    return process.exitValue() ? 'unknown' : process.text.trim();
}

publishing {
    repositories {
        def githubToken = System.getenv("GITHUB_TOKEN")
        if (githubToken != null) {
            maven {
                name = "GitHubPackages"
                url = project.uri(project.property("ghUri") + project.property("ghSlug"))
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = githubToken
                }
            }
        }

        def artifactoryToken = System.getenv("ARTIFACTORY_TOKEN")
        if (artifactoryToken != null) {
            maven {
                name = "Artifactory"
                url = project.uri(System.getenv("ARTIFACTORY_URL"))
                credentials {
                    username = System.getenv("ARTIFACTORY_USER")
                    password = artifactoryToken
                }
            }
        }
    }
}